@language("no-prelude")

import
    Ludwig.Int      as .
    Ludwig.List     as List
    Ludwig.String   as String
    Ludwig.Error    as Error
    Ludwig.Optional as Optional

export
    type Validation
    success
    failure
    validateMany
    successOrError

type Validation:
    | Success
    | Failure List<String>

Validation success: Success

fun failure(err: String) -> Validation: Failure([err])

fun appendValidations(x: Validation, y: Validation) -> Validation:
    case(x,y) of
    | (Success   , Success   ) -> Success
    | (Failure xs, Failure ys) -> Failure(List.concat(xs,ys))
    | (Failure xs, Success   ) -> Failure(xs)
    | (Success   , Failure xs) -> Failure(xs)

fun concatValidations(xs: List<Validation>) -> Validation:
    List.fold1(appendValidations, xs)

fun validateMany(fs: List<fun(a) -> Validation>, x: a) -> Validation:
    let vs: List.map(fun(f): f(x), fs)
    concatValidations(vs)

fun successOrError(result: a, x: Validation) -> a:
    case x of
    | Success -> result
    | Failure xs ->
        case List.uncons(xs) of
        | None ->
            Error.error("Expected at least one error message but got none")
        | Optional (e, es) ->
            # more than one error
            if List.length(es) > 0 then
                Error.errorAt(1,
                    String.join(
                        "\n",
                        List.concat(
                            ["Failed validations:"],
                            List.map(fun(x): String.concat("  - ", x), xs)
                        )
                    )
                )
            # single error
            else
                Error.errorAt(1, e)
