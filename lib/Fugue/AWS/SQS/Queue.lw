export
  type RedrivePolicy
  new

import Fugue.AWS.Region as Region
import Fugue.Core.AWS.SQS as SQS
import Ludwig.External as External

# Queue constructor.
#
# Call this constructor to create a new Fugue.AWS.SQS.Queue value.
#
# Example usage:
#
#     import Fugue.AWS.SQS as SQS
#     import Fugue.AWS.Region as Region
#
#     deadLetter: SQS.Queue.new {
#       name: "dead-letter",
#       region: Region.Us-east-1,
#     }
#     
#     queue: SQS.Queue.new {
#       name: "myQueue",
#       region: Region.Us-east-1,
#       redrivePolicy: {
#         maxReceiveNumber: 5,
#         deadLetterTarget: deadLetter,
#       }
#     }
#
# @arg name The name of the queue.
#
# @arg region The region to create the queue in.
#
# @arg delaySeconds The number of seconds to delay delivery of
# messages in the queue. Must be between 0 and 900. Defaults to 0.
#
# @arg maximumMessageSize The maximum message size in bytes. Must be
# between 1024 and 262144. Defaults to 262144 (256 KiB).
#
# @arg messageRetentionPeriod The number of seconds the message is
# retained in the queue. Must be between 60 and 1209600. Defaults
# to 345600 (four days).
#
# @arg policy The queue's policy.
#
# @arg receiveMessageWaitTimeSeconds The number of seconds that a
# ReceiveMessage call waits for a message to arrive. Must be
# between 0 and 20. Defaults to 0.
#
# @arg redrivePolicy The RedrivePolicy to apply to the queue.
#
# @arg visibilityTimeout The visibility timeout for the queue in
# seconds. Must be between 0 and 43200. Defaults to 30.
#
# @return A queue.
fun new {
      name: String,
      region: Region.Region,
      delaySeconds: Optional<Int>,
      maximumMessageSize: Optional<Int>,
      messageRetentionPeriod: Optional<Int>,
      policy: Optional<String>,
      receiveMessageWaitTimeSeconds: Optional<Int>,
      redrivePolicy: Optional<RedrivePolicy>,
      visibilityTimeout: Optional<Int>
    } -> SQS.Queue:
  let deadLetter: case redrivePolicy of
                    | None       -> None
                    | Optional x -> Optional(SQS.RedrivePolicy {
                                      maxReceiveNumber: x.maxReceiveNumber,
                                      deadLetterTarget: x.deadLetterTarget
                                    })
  SQS.Queue {
    queueName: name,
    region: region,
    delaySeconds: delaySeconds,
    maximumMessageSize: maximumMessageSize,
    messageRetentionPeriod: messageRetentionPeriod,
    policy: policy,
    receiveMessageWaitTimeSeconds: receiveMessageWaitTimeSeconds,
    visibilityTimeout: visibilityTimeout,
    redrivePolicy: deadLetter,
  }

# A dead letter policy for a queue.
#
# @field maxReceiveNumber The number of times a message can be
# received before being forwarded to the deadLetterTarget. Must be
# between 0 and 1000.
#
# @field deadLetterTarget The queue to deliver messages that cannot be
# processed to.
type RedrivePolicy:
  maxReceiveNumber: Int
  deadLetterTarget: SQS.Queue

# Create a reference to an externally managed Queue.
#
# Example usage:
#
#     queue: SQS.external("myQueue", AWS.Us-east-1)
#
# @arg name The name of the queue to reference.
#
# @arg region The region containing the external queue.
#
# @return A reference to the specific Queue.
fun external(name: String, region: Region.Region) -> SQS.Queue:
  External.external {
    value: name,
    namespace: Region.toString(region)
  }

