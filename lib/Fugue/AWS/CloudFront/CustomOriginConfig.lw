# This is the preferred interface for making CloudFront CustomOriginConfigs
# with Fugue. If you want to manage an CloudFront CustomOriginConfigs with 
# Fugue, the `new` function is the right place to start.

export
  new

import Fugue.AWS.Internal.Validation as V
import Fugue.AWS.ResourceID as ResourceID
import Fugue.AWS.CloudFront.Validators as .
import Fugue.Core.AWS.Common as AWS
import Fugue.Core.AWS.CloudFront as CF

# `new` CloudFront CustomOriginConfig (Constructor)
#
# Call this constructor to create a new 
# Fugue.Core.AWS.CloudFront.CustomOriginConfig value.
#
# @arg httpPort The HTTP port the custom origin listens on.
#
# @arg httpsPort The HTTPS port the custom origin listens on.
#
# @arg originProtocolPolicy A CloudFront.OriginProtocolPolicy value that 
# specifies the origin protocol policy to apply to your origin.
#
# @arg originSslProtocols A CloudFront.SslProtocol value that specifies the 
# SSL/TLS protocols that you want CloudFront to use when communicating with
# your origin over HTTPS.
#
# @return a Fugue.Core.AWS.CloudFront.CustomOriginConfig value.
fun new {
      httpPort: Int,
      httpsPort: Int,
      originProtocolPolicy: CF.OriginProtocolPolicy,
      originSslProtocols: Optional<List<CF.SslProtocol>>
    } -> CF.CustomOriginConfig:
  let valid: V.concat [
    validatePort(httpPort, "http"),
    validatePort(httpsPort, "https")
  ]
  case valid of 
    | V.Success -> CF.CustomOriginConfig {
        httpPort: httpPort,
        httpsPort: httpsPort,
        originProtocolPolicy: originProtocolPolicy,
        originSslProtocols: originSslProtocols
      }
    | V.Fail err -> error("Invalid CustomOriginConfig: " ++ err)
