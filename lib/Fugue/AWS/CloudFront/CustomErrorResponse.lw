# This is the preferred interface for making CloudFront CustomErrorResponses
# with Fugue. If you want to manage an CloudFront CustomErrorResponses with 
# Fugue, the `new` function is the right place to start.

export
  new

import Fugue.AWS.Internal.Validation as V
import Fugue.AWS.CloudFront.Validators as .
import Fugue.Core.AWS.CloudFront as CF

# `new` CloudFront CustomErrorResponse (Constructor)
#
# Call this constructor to create a new 
# Fugue.Core.AWS.CloudFront.CustomErrorResponse value.
#
# @arg errorCode The 4xx or 5xx HTTP status code that you want to customize. 
#
# @arg responsePagePath The path of the custom error page 
# (for example, /custom_404.html). The path is relative to the distribution and 
# must begin with a slash (/). 
# 
# @arg responseCode The HTTP status code that you want CloudFront to return with 
# the custom error page to the viewer. 
#
# @arg errorCachingMinTTL The minimum amount of time you want HTTP error codes
# to stay in CloudFront caches before CloudFront queries your origin to see 
# whether the object has been updated. You can specify a value from
# 0 to 31,536,000.
#
# @return a Fugue.Core.AWS.CloudFront.CustomErrorResponse value.
fun new {
      errorCode: Int,
      responsePagePath: Optional<String>,
      responseCode: Optional<String>,
      errorCachingMinTTL: Optional<Int>
    } -> CF.CustomErrorResponse:
  let valid: V.concat [
    validateTTL(Optional.unpack(0, errorCachingMinTTL), "ErrorCachingMinTTL")
  ]
  case valid of 
    | V.Success -> CF.CustomErrorResponse {
      errorCode: errorCode,
      responsePagePath: responsePagePath,
      responseCode: responseCode,
      errorCachingMinTTL: errorCachingMinTTL
      }
    | V.Fail err -> error("Invalid CustomErrorResponse: " ++ err)

