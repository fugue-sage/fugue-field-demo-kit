# This is the preferred interface for making CloudFront DefaultCacheBehaviors
# with Fugue. If you want to manage an CloudFront DefaultCacheBehaviors with 
# Fugue, the `new` function is the right place to start.

export
  new

import Fugue.AWS.CloudFront.Validators as .
import Fugue.AWS.Internal.Validation as V
import Fugue.AWS.ResourceID as ResourceID
import Fugue.Core.AWS.Common as AWS
import Fugue.Core.AWS.CloudFront as CF

# `new` CloudFront DefaultCacheBehavior (Constructor)
#
# Call this constructor to create a new 
# Fugue.Core.AWS.CloudFront.DefaultCacheBehavior value.
#
# @arg forwardedValues A CloudFront.ForwardedValues value that specifies how 
# CloudFront handles query strings, cookies and headers.
#
# @arg trustedSigners A CloudFront.TrustedSigners value that specifies the AWS 
# accounts, if any, that you want to allow to create signed URLs for private 
# content.
#
# @arg viewerProtocolPolicy A CloudFront.ViewerProtocolPolicy value that 
# specifies the protocol that users can use to access the files in the origin
# specified by TargetOriginId when a request matches the path pattern in
# PathPattern.
#
# @arg minTTL The minimum amount of time that you want objects to stay in
# CloudFront caches before CloudFront queries your origin to see whether the 
# object has been updated. You can specify a value from 0 to 3,153,600,000 
# seconds (100 years).
#
# @arg allowedMethods a CloudFront.AllowedMethods value that controls which 
# HTTP methods CloudFront processes and forwards to your Amazon S3 bucket or 
# your custom origin. 
#
# @arg smoothStreaming Indicates whether you want to distribute media files in
# Microsoft Smooth Streaming format using the origin that is associated with
# this cache behavior. 
#
# @arg defaultTTL the default amount of time (in seconds) that an object is in a
# CloudFront cache before CloudFront forwards another request to your origin to
# determine whether the object has been updated.
#
# @arg maxTTL The maximum amount of time (in seconds) that an object is in a 
# CloudFront cache before CloudFront forwards another request to your origin to 
# determine whether the object has been updated. 
#
# @arg compress Whether you want CloudFront to automatically compress content 
# for web requests that include Accept-Encoding: gzip in the request header. 
#
# @arg targetOrigin the CloudFront.Origin to which this 
# DefaultCacheBehavior applies
#
# @return a Fugue.Core.AWS.CloudFront.DefaultCacheBehavior value.
fun new {
      forwardedValues: CF.ForwardedValues,
      trustedSigners: CF.TrustedSigners,
      viewerProtocolPolicy: CF.ViewerProtocolPolicy,
      minTTL: Int,
      allowedMethods: Optional<CF.AllowedMethods>,
      smoothStreaming: Optional<Bool>,
      defaultTTL: Optional<Int>,
      maxTTL: Optional<Int>,
      compress: Optional<Bool>,
      targetOrigin: CF.Origin
    } -> CF.DefaultCacheBehavior:
  let valid: V.concat [
    validateTTL(minTTL, "MinTTL"),
    validateTTL(Optional.unpack(0, defaultTTL), "DefaultTTL"),
    validateTTL(Optional.unpack(0, maxTTL), "MaxTTL")
  ]
  case valid of 
    | V.Success -> CF.DefaultCacheBehavior {
        forwardedValues: forwardedValues,
        trustedSigners: trustedSigners,
        viewerProtocolPolicy: viewerProtocolPolicy,
        minTTL: minTTL,
        allowedMethods: allowedMethods,
        smoothStreaming: smoothStreaming,
        defaultTTL: defaultTTL,
        maxTTL: maxTTL,
        compress: compress,
        targetOrigin: targetOrigin
      }
    | V.Fail err -> error("Invalid DefaultCacheBehavior: " ++ err)
