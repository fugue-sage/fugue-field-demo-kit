export
  type Value
  new

import Fugue.AWS.Route53.ResourceRecordSet.Base as ResourceRecordSet
import Fugue.Core.AWS.Route53 as Route53
import Ludwig.Int as Int

# See https://en.wikipedia.org/wiki/MX_record for more details on the
# MX record format.
#
# @field priority The priority of the record entry.
#
# @field hostname The fully qualified domain name of the target mail
# server.
type Value:
  priority: Int
  hostname: String

# Create a new MX ResourceRecordSet.
#
# Example usage:
#
#     import Fugue.AWS.Route53 as Route53
#     
#     zone: Route53.HostedZone.external("12345679ABCDEF")
#     
#     mx: Route53.ResourceRecordSet.MX.new {
#       name: "foo.example.com",
#       values: [
#         {priority: 10, hostname: mail1.example.com},
#         {priority: 20, hostname: mail2.example.com},
#       ],
#       ttl: 60,
#       hostedZone: zone
#     }
#
# @arg name The fully qualified domain name of the record set.
#
# @arg values The list of values specifying the mail servers and
# their priority.
#
# @arg ttl The time to live in seconds.
#
# @arg hostedZone The hosted zone to create the record in.
fun new {
      name: String,
      values: List<Value>,
      ttl: Int,
      hostedZone: Route53.HostedZone
    } -> Route53.ResourceRecordSet:
  let toRR: fun(x): Int.toString(x.priority) ++ " " ++ x.hostname
  ResourceRecordSet.new {
    name: name,
    recordType: Route53.MX,
    ttl: ttl,
    hostedZone: hostedZone,
    resourceRecords: List.map(toRR, values),
  }
