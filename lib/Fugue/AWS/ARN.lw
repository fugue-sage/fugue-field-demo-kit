export
  external
  region
  service

import Fugue.AWS.Region as Region
import Ludwig.External as External

# TODO: Document this type further, with an example.

type ARN:
  partition: String
  service: String
  region: Optional<Region.Region>
  accountId: Optional<String>
  resource: String

# TODO(grim): Handle optional AccountId field in the parser.
fun fromString(arn: String) -> Optional<ARN>:
  let tokens: String.split(":", arn)
  let len: List.length(tokens)
  if len == 6 || len == 7 then
    let regionToken: List.elementAt(3, tokens)
    let region: Region.fromString(regionToken)
    let validRegion: case (region, regionToken) of
                       | (Optional _, _) -> True
                       | (None, "")      -> True
                       | _               -> False
    if validRegion then
      Optional {
        partition: List.elementAt(1, tokens),
        service: List.elementAt(2, tokens),
        region: region,
        accountId: List.elementAt(4, tokens),
        resource: let idx: if len == 6 then 5 else 6
                  List.elementAt(idx, tokens)
      }
    else None
  else None

fun service(arn: String) -> String:
  case fromString(arn) of
    | Optional parsed -> parsed.service
    | None            -> error("Invalid arn: '" ++ arn ++ "'")

fun region(arn: String) -> Optional<Region.Region>:
  case fromString(arn) of
    | None            -> error("Invalid arn: '" ++ arn ++ "'")
    | Optional parsed -> parsed.region

fun external(arn: String) -> resource:
  case fromString(arn) of
    | None            -> error("Invalid arn: '" ++ arn ++ "'")
    | Optional parsed -> External.external {
                           # TODO(grim): This is to get around a bug
                           # in lwc. The `'' ++` here should be
                           # removed once it's written up and fixed.
                           value: '' ++ arn,
                           namespace: Optional.mapUnpack("global", Region.toString, parsed.region),
                         }
