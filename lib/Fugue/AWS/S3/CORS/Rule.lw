export
  new

import Fugue.AWS.Internal.Validation as V
import Fugue.Core.AWS.S3 as S3

fun new {
      allowedOrigins: List<String>,
      allowedMethods: List<String>,
      allowedHeaders: Optional<List<String>>,
      exposeHeaders: Optional<List<String>>,
      maxAgeSeconds: Optional<Int>
    } -> S3.CORSRule:
  let valid: V.concat [
    atLeastOneOrigin(allowedOrigins),
    atLeastOneMethod(allowedMethods),
    oneWildcardPerOrigin(allowedOrigins),
    oneWildcardPerAllowedHeader(Optional.unpack([], allowedHeaders)),
  ]
  case valid of
    | V.Fail err -> error("Invalid CORS Rule: " ++ err)
    | V.Success  -> S3.CORSRule {
                      allowedOrigins: allowedOrigins,
                      allowedMethods: allowedMethods,
                      allowedHeaders: allowedHeaders,
                      exposeHeaders: exposeHeaders,
                      maxAgeSeconds: maxAgeSeconds,
                    }

fun atLeastOneOrigin(allowedOrigins: List<String>) -> V.Validation:
  if List.null(allowedOrigins) then
    V.Fail("At least one allowed origin must be specified")
  else
    V.Success

fun atLeastOneMethod(allowedMethods: List<String>) -> V.Validation:
  if List.null(allowedMethods) then
    V.Fail("At least one allowed method must be specified")
  else
    V.Success

fun oneWildcardPerOrigin(origins: List<String>) -> V.Validation:
  let invalid: List.filter(multipleWildcards, origins)
  if List.null(invalid) then
    V.Success
  else
    V.Fail("Origins may contain at most one wildcard character")

fun oneWildcardPerAllowedHeader(headers: List<String>) -> V.Validation:
  let invalid: List.filter(multipleWildcards, headers)
  if List.null(invalid) then
    V.Success
  else
    V.Fail("Allowed headers may contain at most one wildcard character")

fun multipleWildcards(str: String) -> Bool:
  let chars: String.split("", str)
  let wildcards: List.filter(fun(c): String.equal(c, "*"), chars)
  List.length(wildcards) > 1
