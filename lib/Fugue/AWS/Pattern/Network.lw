export
  type Network
  new
  public

import Fugue.AWS as AWS
import Fugue.AWS.EC2 as EC2

# @field vpc The underlying vpc.
# @field publicSubnets The list of public subnets.
# @field privateSubnets The list of private subnets.
# @field igw The underlying internet gateway.
# @field routes The list of route tables created for the network.
# @field region The region in which the network is deployed.
type Network:
  vpc: EC2.Vpc
  publicSubnets: List<EC2.Subnet>
  privateSubnets: List<EC2.Subnet>
  igw: EC2.InternetGateway
  routes: List<EC2.RouteTable>
  region: AWS.Region

# Network constructor.
#
# Creates a two tiered network with public and private subnets.
#
# Example:
#
#     import Fugue.AWS.Pattern.Network as Network
#     import Fugue.Core.AWS.Common as AWS
#
#     net: Network.new {
#       name: "my-pub-net",
#       region: AWS.Us-west-2,
#       cidr: "10.0.0.0/16",
#       publicSubnets: [
#         (AWS.A, "10.0.1.0/24"),
#         (AWS.B, "10.0.2.0/24"),
#       ],
#       privateSubnets: [
#         (AWS.A, "10.0.3.0/24"),
#         (AWS.B, "10.0.4.0/24"),
#       ],
#     }
#
# @arg name The base name of the network. Used as the base for
# generating Name tags.
#
# @arg region The region in which to create the network.
#
# @arg cidr The CIDR to use when allocating the underlying Vpc.
#
# @arg publicSubnets A list of AvailabilityZone and network addresses
# in CIDR notation. A public subnet is created for each pair.
#
# @arg privateSubnets A list of AvailabilityZone and network addresses
# in CIDR notation. A private subnet is created for each pair.
#
# @arg dhcpOptions A DhcpOptions record, describing DHCP options for
# the VPC's operation.
#
# @arg enableDnsSupport Set to True to enable DNS support for the Vpc.
#
# @arg enableDnsHostnames Set to True to enable DNS hostnames for the
# Vpc. This can only be enabled if enableDnsSupport is also True.
#
# @return A `Network` value.
fun new {
      name: String,
      region: AWS.Region,
      cidr: String,
      publicSubnets: List<(AWS.AvailabilityZone, String)>,
      privateSubnets: List<(AWS.AvailabilityZone, String)>,
      dhcpOptions: Optional<EC2.DhcpOptions>,
      enableDnsSupport: Optional<Bool>,
      enableDnsHostnames: Optional<Bool>
    } -> Network:
  let vpc: EC2.Vpc.new {
    cidrBlock: cidr,
    tags: [AWS.Tag({key: "Name", value: name})],
    region: region,
    dhcpOptions: dhcpOptions,
    enableDnsSupport: enableDnsSupport,
    enableDnsHostnames: enableDnsHostnames,
  }
  let igw: EC2.InternetGateway.new {
    vpc: vpc,
    tags: [
      AWS.tag("Name", name ++ "-IGW"),
      AWS.tag("network", "public")
    ]
  }
  let pubSubnets: mkPublicSubnets(name, vpc, publicSubnets)
  let privSubnets: mkPrivateSubnets(name, vpc, privateSubnets)
  {
    vpc: vpc,
    publicSubnets: pubSubnets,
    privateSubnets: privSubnets,
    igw: igw,
    routes: [publicRoutes(name, vpc, igw, pubSubnets)],
    region: region,
  }


fun mkPublicSubnets(name: String, vpc: EC2.Vpc, subnets: List<(AWS.AvailabilityZone, String)>) -> List<EC2.Subnet>:
  let makePub:
    fun(s):
      case s of
        | (az, cidr) -> subnet(subnetName(name, "PUBLIC", az), cidr, vpc, az, True)

  List.map(makePub, subnets)

fun mkPrivateSubnets(name: String, vpc: EC2.Vpc, subnets: List<(AWS.AvailabilityZone, String)>) -> List<EC2.Subnet>:
  let makePriv:
    fun(s):
      case s of
        | (az, cidr) -> subnet(subnetName(name, "PRIVATE", az), cidr, vpc, az, False)

  List.map(makePriv, subnets)

fun subnet(name: String, cidr: String, vpc: EC2.Vpc, az: AWS.AvailabilityZone, public: Bool) -> EC2.Subnet:
  EC2.Subnet.new {
    vpc: vpc,
    cidrBlock: cidr,
    availabilityZone: az,
    mapPublicIpOnLaunch: public,
    tags: [
      AWS.tag("Name", name),
      AWS.tag("network", if public then "public" else "private")
    ]
  }

fun azToString(az: AWS.AvailabilityZone) -> String:
  case az of
    | AWS.A -> "A"
    | AWS.B -> "B"
    | AWS.C -> "C"
    | AWS.D -> "D"
    | AWS.E -> "E"

fun subnetName(base: String, access: String, az: AWS.AvailabilityZone) -> String:
  base ++ "-" ++ access ++ "-SN-" ++ azToString(az)

fun publicRoutes(name: String, vpc: EC2.Vpc, igw: EC2.InternetGateway, publicSubnets: List<EC2.Subnet>) -> EC2.RouteTable:
  EC2.RouteTable.new {
    vpc: vpc,
    associations: publicSubnets,
    routes: [
      EC2.Route.new {
        destinationCidrBlock: "0.0.0.0/0",
        target: EC2.GatewayTarget(igw)
      }
    ],
    tags: [
      AWS.tag("Name", name ++ "-PUBLIC-RT"),
      AWS.tag("network", "public")
    ]
  }

# Public Network constructor.
#
# Creates a network with only public subnets.
#
# Example:
#
#     import Fugue.AWS.Pattern.Network as Network
#     import Fugue.Core.AWS.Common as AWS
#     
#     net: Network.public {
#       name: "my-pub-net",
#       region: AWS.Us-west-2,
#       cidr: "10.0.0.0/16",
#       subnets: [
#         (AWS.A, "10.0.1.0/24"),
#         (AWS.B, "10.0.2.0/24"),
#       ]
#     }
#
# @arg name The base name of the network. Used as the base for
# generating Name tags.
#
# @arg region The region in which to create the network.
#
# @arg cidr The CIDR to use when allocating the underlying Vpc.
#
# @arg subnets A list of AvailabilityZone and network addresses in
# CIDR notation. A public subnet is created for each pair.
#
# @arg dhcpOptions A DhcpOptions record, describing DHCP options for
# the VPC's operation.
#
# @arg enableDnsSupport Set to True to enable DNS support for the Vpc.
#
# @arg enableDnsHostnames Set to True to enable DNS hostnames for the
# Vpc. This can only be enabled if enableDnsSupport is also True.
#
# @return A `Network` with only public subnets.
fun public {
      name: String,
      region: AWS.Region,
      cidr: String,
      subnets: List<(AWS.AvailabilityZone, String)>,
      dhcpOptions: Optional<EC2.DhcpOptions>,
      enableDnsSupport: Optional<Bool>,
      enableDnsHostnames: Optional<Bool>
    } -> Network:
  new {
    name: name,
    region: region,
    cidr: cidr,
    publicSubnets: subnets,
    privateSubnets: [],
    dhcpOptions: dhcpOptions,
    enableDnsSupport: enableDnsSupport,
    enableDnsHostnames: enableDnsHostnames,
  }
