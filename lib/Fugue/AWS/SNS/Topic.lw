import Fugue.AWS.ARN as ARN
import Fugue.AWS.Region as Region
import Fugue.Core.AWS.SNS as SNS

# Topic Constructor
#
# Call this constructor to create a new Fugue.AWS.SNS.Topic value.
#
# Example Usage:
#
#     topic: SNS.Topic.new {
#       name: "example-topic",
#       region: AWS.Us-east-1,
#     }
#
# @arg name The name to give the topic. Must be between 1 and 256
# characters and made up of ASCII letters, numbers, underscores and
# hyphens.
#
# @arg region The region to create the topic in.
#
# @arg policy The IAM policy to attach to the topic.
#
# @return A topic.
fun new {name: String, region: Region.Region, policy: Optional<String>} -> SNS.Topic:
  let len: String.length(name)
  if (len < 1) || (len > 256) then
    error("Invalid Topic: name must be between 1 and 256 characters in length.")
  else
    SNS.Topic {
      name: name,
      region: region,
      policy: policy,
    }

# Create a reference to an externally managed Topic.
#
# Example usage:
#
#     topic: SNS.Topic.external("arn:aws:sns:us-east-1:123456789012:example-topic")
#
# @arg arn The ARN of an existing topic.
#
# @return A reference to the specified Topic.
fun external(arn: String) -> SNS.Topic:
  if String.equal(ARN.service(arn), "sns") then ARN.external(arn)
  else error("Invalid SNS arn: '" ++ arn ++ "'")
