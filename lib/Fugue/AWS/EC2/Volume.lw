# This is the preferred interface for making EBS Volumes with
# Fugue. The `new` function is the right place to start.

export
  type Spec
  default
  external
  new

import Fugue.AWS.ResourceID as ResourceID
import Fugue.Core.AWS.Common as AWS
import Fugue.Core.AWS.EC2 as EC2

# `new` EBS Volume (Constructor)
#
# Call this constructor to create a new
# Fugue.Core.AWS.EC2.Volume value.
#
# @arg spec A EBS Volume Specification record.
#
# @return A Fugue.Core.AWS.EC2.Volume value.
fun new(spec: Spec) -> EC2.Volume: EC2.Volume(spec)

# EBS Volume Specification (Resource)
#
# @field size The size of the volume in gibibytes (GiB).
#
# @field availabilityZone The availability zone in which to place the volume.
#
# @field tags The AWS tags to apply to the volume.
#
# @field volumeType The type of volume, such as General Purpose or Provisioned IOPs.
#
# @field iops The IOPs to provision for the volume. If the volume is
# of the Provisioned IOPs type, this is required.
#
# @field encrypted Whether or not the volume should be encrypted.
#
# @field snapshot A snapshot from which to create the volume.
type Spec:
  size: Optional<Int>
  availabilityZone: AWS.AvailabilityZone
  tags: Optional<List<AWS.Tag>>
  volumeType: Optional<EC2.VolumeType>
  iops: Optional<Int>
  encrypted: Optional<Bool>
  snapshot: Optional<String>

# EBS Volume Specification (Default Values)
default: {
  tags: None,
  volumeType: None,
  iops: None,
  encrypted: None,
  snapshot: None,
}

# Create a reference to an externally managed Volume.
#
# Example usage:
#
#     volume: EC2.Volume.external("vol-1234abcd", AWS.Us-east-1)
#
# @arg volumeId The ID of the target Volume. Must be of the form
#               "vol-" followed by 8 characters from a-z and 0-9.
#
# @arg region The Region containing the target Volume.
#
# @return A reference to the specified Volume.
fun external(volumeId: String, region: AWS.Region) -> EC2.Volume:
  ResourceID.external(volumeId, region, "vol")
