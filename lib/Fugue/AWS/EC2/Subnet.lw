# This is the preferred interface for making Subnets with Fugue. The
# `new` function is the right place to start.

export
  type SubnetSpec
  default
  external
  new

import Fugue.AWS.ResourceID as ResourceID
import Fugue.Core.AWS.Common as AWS
import Fugue.Core.AWS.EC2 as EC2
import Fugue.NetAddr as Net
import Ludwig.External as External

# `new` Subnet (Constructor)
#
# Call this constructor to create a new Fugue.AWS.EC2.Subnet value.
#
# Example usage:
#
#     sn: EC2.Subnet.new {
#       cidrBlock: '10.0.1.0/24',
#       vpc: myVpc
#       availabilityZone: AWS.A,
#       mapPublicIpOnLaunch: True,
#       defaultForAz: False,
#       tags: [myTag]
#     }
#
# @arg spec A Subnet Specification record.
#
# @return A Fugue.Core.AWS.EC2.Subnet record.
fun new(spec: SubnetSpec) -> EC2.Subnet:
  let cidrBlock: cidr(spec.cidrBlock)
  let valid:
    isValidCidr(cidrBlock) &&
    isValidCidrForVpc(cidrBlock, spec.vpc)
  if valid then EC2.Subnet(spec) else error("Invalid SubnetSpec")

# Subnet Specification (Resource)
#
# This type of value is used to specify a Subnet for the Subnet
# constructor, EC2.Subnet.`new`().
#
# @field vpc The VPC in which to build the subnet. This must be a
#   valid VPC record, constructed with the EC2.Vpc() constructor.
#
# @field cidrBlock The network address space the subnet should
#   encompass. This value is validated at compile time by the
#   constructor, and must be a string written in CIDR notation. For
#   more information see:
#   https://en.wikipedia.org/wiki/Classless_Inter-Domain_Routing#CIDR_notation
#
# @field availabilityZone The availability zone in which the subnet
#   resides. Instances launched in this subnet must also be launched
#   in this availability zone. Of course, this must be a valid
#   availability zone for the region in which the parent VPC is
#   placed.
#
# @field defaultForAz The setting for whether this subnet is the
#   default for instances launched in its Availability Zone.
#
# @field mapPublicIpOnLaunch The setting for whether instances
#   launched in this subnet are mapped to public IP addresses on
#   launch.
#
# @field tags AWS tag key-value pairs to associate with the VPC.
type SubnetSpec:
  vpc: EC2.Vpc
  cidrBlock: String
  availabilityZone: Optional<AWS.AvailabilityZone>
  defaultForAz: Optional<Bool>
  mapPublicIpOnLaunch: Optional<Bool>
  tags: Optional<List<AWS.Tag>>

# Subnet Specification Defaults
default: {
  defaultForAz: None,
  mapPublicIpOnLaunch: None,
  tags: None
}

# CIDR Constructor
#
# *NOTE:* This is a private function, and cannot be used outside of
# *this module.
#
# This function is used to construct a `Net.Cidr` record from an
# `Optional<String>` value.
#
# @arg cidrBlock A possible CIDR string.
#
# @return Net.Cidr
fun cidr(cidrBlock: String) -> Net.Cidr:
  case Net.Cidr.fromString(cidrBlock) of
    | Optional cidr -> cidr
    | None          -> error(String.concat("Invalid cidrBlock: '", String.concat(cidrBlock, "'")))

# (isValidCidr) Test
#
# *NOTE:* This is a private function, and cannot be used outside of
# *this module.
#
# This function can be used to test the validity of a CIDR block for
# use with a Subnet. In general, CIDRs that are between a /28 and /16
# netmask are valid. It is automatically used by the Subnet
# constructor function.
#
# @arg cidr A CIDR block expressed with valid notation. For more
#   information, see:
#   https://en.wikipedia.org/wiki/Classless_Inter-Domain_Routing#CIDR_notation
#
# @return Bool
fun isValidCidr(net: Net.Cidr) -> Bool:
  if net.mask >= 16 && net.mask <= 28 then True
  else error("Subnet cidrBlock size must be between a /28 and /16 netmask.")

# (isValidCidrForVpc) Test
#
# *NOTE:* This is a private function, and cannot be used outside of
# *this module.
#
# This function can be used to determine if a subnet CIDR block is
# valid for a given VPC. To be valid, the subnet CIDR block must be
# smaller than the VPC CIDR block, and thus able to be included within
# the VPC.
#
# @arg subnetBlock The CIDR block of the subnet.
#
# @arg vpc The VPC to test the subnet against.
#
# @return Bool 
fun isValidCidrForVpc(subnetBlock: Net.Cidr, vpc: EC2.Vpc) -> Bool:
  if External.isExternal(vpc) then
     # we can't verify an external vpc, so we just have to trust that
     # it's valid.
    True
  else
    let network: case vpc of | EC2.Vpc v -> cidr(v.cidrBlock)
    if Net.Cidr.include(subnetBlock.network, network) then True
    else
      let msg:
        List.fold(
          String.concat,
          "",
          [
            "Subnet cidrBlock '",
            Net.Cidr.toString(subnetBlock),
            "' must be a subset of the Vpc cidrBlock '",
            Net.Cidr.toString(network), "'"
          ]
        )
      error(msg)

# Create a reference to an externally managed Subnet.
#
# Example usage:
#
#     subnet: EC2.Subnet.external("subnet-1234abcd", AWS.Us-east-1)
#
# @arg subnetId The ID of the target Subnet. Must be of the form
#               "subnet-" followed by 8 characters from a-z and 0-9.
#
# @arg region The Region containing the target Subnet.
#
# @return A reference to the specified Subnet.
fun external(subnetId: String, region: AWS.Region) -> EC2.Subnet:
  ResourceID.external(subnetId, region, "subnet")
