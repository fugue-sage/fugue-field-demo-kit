# This is the preferred interface for making Security Groups with
# Fugue. The `new` function is the right place to start.

export
  type SecurityGroupSpec
  default
  external
  new

import Fugue.AWS.ResourceID as ResourceID
import Fugue.Core.AWS.Common as AWS
import Fugue.Core.AWS.EC2 as EC2

# `new` Security Group (Constructor)
#
# Call this constructor to create a new Fugue.AWS.EC2.SecurityGroup value.
#
# @arg spec A SecurityGroupSpec record.
#
# @return A Fugue.Core.AWS.EC2.SecurityGroup record.
#
# Example usage:
#
#     application-sg: EC2.SecurityGroup.new {
#       description: "Allow http/s traffic from the Internet",
#       ipPermissions: [
#         EC2.IpPermission.http(EC2.IpPermission.Target.all),
#         EC2.IpPermission.https(EC2.IpPermission.Target.all),
#       ],
#       vpc: production-vpc
#     }
fun new(spec: SecurityGroupSpec) -> EC2.SecurityGroup:
  let valid:
    isValidDescription(spec.description)
  if valid then EC2.SecurityGroup(spec) else error("Invalid SecurityGroupSpec")

# Security Group Specification (Resource)
#
# This type of value is used to specify a security group for the
# SecurityGroup constructor, EC2.SecurityGroup.`new`(). Use the
# functions in `Fugue.AWS.EC2.IpPermission.Target` to construct the
# values for `ipPermissions` and `ipPermissionsEgress`.
#
# @field description A useful description of the security group.
#
# @field ipPermissions A list of ingress rules, expressed as
# `Fugue.Core.AWS.EC2.IpPermission` values.
#
# @field ipPermissionsEgress A list of egress rules, expressed as
# `Fugue.Core.AWS.EC2.IpPermission` values.
#
# @field vpc The VPC with which to associate the security group. Only
# instances launched within this VPC may have this security group
# applied to them.
#
# @field tags AWS tag key-value pairs to associate with the security
# group.
type SecurityGroupSpec:
  description: String
  ipPermissions: Optional<List<EC2.IpPermission>>
  ipPermissionsEgress: Optional<List<EC2.IpPermission>>
  vpc: EC2.Vpc
  tags: Optional<List<AWS.Tag>>

# Security Group Specification (Default Values)
default: {
  ipPermissions: None,
  ipPermissionsEgress: None,
  tags: None
}

# (isValidDescription) Test
#
# *NOTE:* This is a private function, and cannot be used outside of
# *this module.
#
# This function can be used to test the validity of a security group
# description. In general, strings of up to 255 characters in length
# are valid security group descriptions.
#
# @arg description The description string to test.
#
# @return Bool
fun isValidDescription(desc: String) -> Bool:
  if String.length(desc) <= 255 then True
  else error("Description may only be up to 255 characters in length.")

# Create a reference to an externally managed SecurityGroup.
#
# Example usage:
#
#     sg: EC2.SecurityGroup.external("sg-1234abcd", AWS.Us-east-1)
#
# @arg securityGroupId The ID of the target SecurityGroup. Must be of
#                      the form "sg-" followed by 8 characters from
#                      a-z and 0-9.
#
# @arg region The Region containing the target SecurityGroup.
#
# @return A reference to the specified SecurityGroup.
fun external(securityGroupId: String, region: AWS.Region) -> EC2.SecurityGroup:
  ResourceID.external(securityGroupId, region, "sg")
