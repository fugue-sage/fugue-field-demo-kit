export
  type PolicySpec
  external
  new
  module AssumeRole as AssumeRole

import Fugue.AWS.IAM.Policy.AssumeRole as AssumeRole
import Fugue.Core.AWS.IAM as IAM
import Ludwig.External as External

# IAM Policy Specification
#
# This type specifies an IAM policy, a set of permissions that can be
# applied to identities (users, groups, and roles) or resources.
# 
# @field policyName The name of the policy.
#
# @field policyDocument The policy document in JSON format. See
#      [the IAM documentation](http://docs.aws.amazon.com/IAM/latest/UserGuide/reference_policies.html)
# for more details on the format of the document.
#
# You may alternatively attach the policyDocument policy as an external JSON 
# document, using the readFileUtf8() method. Files are relative to the 
# directory in which the lwc compiler is run, unless an absolute path is 
# specified.
#
# Example usage: 
#     policyDocument: String.readFileUtf8("policy/my-policy.json")
type PolicySpec:
  policyName: String
  policyDocument: String

# Policy Constructor
#
# Call this constructor to create a new Fugue.AWS.IAM.Policy value.
# 
# Example usage:
#
#     import Fugue.AWS.IAM as IAM
#     
#     myPolicy: IAM.Policy.new {
#       policyName: "my-policy",
#       policyDocument: String.readFileUtf8("policy/my-policy.json")
#     }
#     
#     anotherPolicy: IAM.Policy.new {
#       policyName: "another-policy",
#       policyDocument: '{
#          "Version": "2012-10-17",
#          "Statement": [
#            {
#              "Effect": "Allow",
#              "Action": ["s3:ListBucket"],
#              "Resource": ["arn:aws:s3:::test"]
#            },
#            {
#             "Effect": "Allow",
#              "Action": [
#                "s3:GetObject",
#              ],
#              "Resource": ["arn:aws:s3:::test/*"]
#            }
#          ]
#        }'
#     }
#
# @arg spec An IAM Policy record.
# @return A Fugue.AWS.IAM.Policy record.
fun new(spec: PolicySpec) -> IAM.Policy: IAM.Policy(spec)

# Create a reference to an externally managed Policy.
#
# Example usage:
#
#     policy: IAM.Policy.external("arn:aws:iam::123456789012:policy/MyPolicy")
#
# @arg arn The Amazon Resource Name (ARN) of the target Policy. Must
#          be at least 20 characters and at most 2048.
#
# @return A reference to the specified Policy.
fun external(arn: String) -> IAM.Policy:
  External.external {value: arn, namespace: "global"}
