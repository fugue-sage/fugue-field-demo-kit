export
  type UserSpec
  default
  new

import Fugue.Core.AWS.IAM as IAM

# IAM User Specification
#
# This type of value specifies an IAM user, a name, and a set of credentials that 
# represent a person or service who interacts with AWS.
#
# @field userName The name of the user.
#
# @field inlinePolicies An optional list of policies to attach to the user.
type UserSpec:
  userName: String
  inlinePolicies: Optional<List<IAM.Policy>>

# IAM User Specification Defaults
#
# These are the default values for an IAM user created with the IAM user
# constructor, IAM.User(). If these values are provided to the constructor,
# the default values are overwritten. The default values are:
#
# - `inlinePolicies`: `None`, meaning no policies are attached to the user.
default: {
  inlinePolicies: None
}

# User Constructor
#
# Call this constructor to create a new Fugue.AWS.IAM.User value.
#
# Example usage:
#
#     import Fugue.Core.AWS.IAM as IAM
#     
#     readFromS3: '{
#       "Version": "2012-10-17",
#       "Statement": [
#         {
#           "Effect": "Allow",
#           "Action": ["s3:ListBucket"],
#           "Resource": ["arn:aws:s3:::example"]
#         },
#         {
#           "Effect": "Allow",
#           "Action": [
#             "s3:GetObject",
#           ],
#           "Resource": ["arn:aws:s3:::example/*"]
#         }
#       ]
#     }'
#     
#     user: IAM.User {
#       userName: "bob",
#       inlinePolicies: [
#         IAM.Policy {
#           policyName: "read-from-s3",
#           policyDocument: readFromS3
#         }
#       ]
#     }
#
# @arg spec An IAM User record.
# @return A Fugue.AWS.IAM.User record.
fun new(spec: UserSpec) -> IAM.User: IAM.User(spec)
