export
  type Spec
  external
  new

import Fugue.Core.AWS.EC2 as EC2
import Fugue.AWS.Region as Region
import Fugue.Core.AWS.Common as AWS
import Fugue.Core.AWS.Elasticache as Elasticache
import Ludwig.External as External

# ReplicationGroup Specification
#
# @field description Description of the replication group.
#
# @field numCacheClusters The number of CacheClusters in the
#                         replication group.
#
# @field cacheNodeType The node type to use in cache clusters.
#
# @field engine The cache engine to use for the replication group.
#
# @field securityGroups The list of SecurityGroups to associate with
#                       the replication group.
#
# @field tags The list of tags to attach to the replication group.
#
# @field port The cache port number.
#
# @field name The replication group identifier. Up to 20 lower case
#             alphanumeric characters or hyphens. Must start with a
#             letter. Cannot end with a hyphen or include two
#             consecutive hyphens.
#
# @field cacheSubnetGroup The CacheSubnetGroup to deploy CacheClusters
#                         in.
type Spec:
  description: String
  numCacheClusters: Optional<Int>
  cacheNodeType: Elasticache.CacheNodeType
  engine: Elasticache.EngineType
  securityGroups: List<EC2.SecurityGroup>
  tags: Optional<List<AWS.Tag>>
  port: Optional<Int>
  name: String
  cacheSubnetGroup: Elasticache.CacheSubnetGroup

# ReplicationGroup Constructor.
#
# Example usage:
#
#     import Fugue.AWS.EC2 as EC2
#     import Fugue.AWS.Elasticache as Elasticache
#     import Fugue.AWS.Pattern.Network as Network
#     import Fugue.Core.AWS.Common as AWS 
#     
#     net: Network.new({
#       name: "EXAMPLE",
#       cidr: "10.42.0.0/16",
#       region: AWS.Us-east-1,
#       publicSubnets: [
#         (AWS.B, "10.42.12.0/24"),
#         (AWS.C, "10.42.13.0/24")
#       ],
#       privateSubnets: [
#         (AWS.B, "10.42.22.0/24"),
#         (AWS.C, "10.42.23.0/24")
#       ]
#     })
#     
#     subnetGroup: Elasticache.CacheSubnetGroup.new {
#       name: "private",
#       description: "private subnets",
#       subnets: net.privateSubnets,
#     }
#     
#     clusterSecurityGroup: EC2.SecurityGroup.new {
#       description: "redis-example-cluster",
#       ipPermissions: [
#         # add rules here...
#       ],
#       ipPermissionsEgress: None,
#       vpc: net.vpc,
#       tags: None
#     }
#     
#     cache: Elasticache.ReplicationGroup.new {
#       name: "example-cache",
#       description: "My cache",
#       cacheNodeType: Elasticache.Cache_t2_micro,
#       engine: Elasticache.Redis,
#       numCacheClusters: 3,
#       securityGroups: [clusterSecurityGroup],
#       port: None,
#       tags: None,
#       cacheSubnetGroup: subnetGroup,
#     }
#
# @arg spec A ReplicationGroup specification.
# @return A ReplicationGroup value.
fun new(spec: Spec) -> Elasticache.ReplicationGroup:
  Elasticache.ReplicationGroup {
    replicationGroupDescription: spec.description,
    numCacheClusters: spec.numCacheClusters,
    cacheNodeType: spec.cacheNodeType,
    engine: spec.engine,
    securityGroups: spec.securityGroups,
    tags: spec.tags,
    port: spec.port,
    replicationGroupName: spec.name,
    cacheSubnetGroup: spec.cacheSubnetGroup,
  }

# Create a reference to an externally managed ReplicationGroup.
#
# Example usage:
#
#     cluster: Elasticache.ReplicationGroup.external("MyCluster", AWS.Us-east-1)
#
# @arg name The name of the target ReplicationGroup.
#
# @arg region The region containing the target ReplicationGroup.
#
# @return A reference to the specified ReplicationGroup.
fun external(name: String, region: Region.Region) -> Elasticache.ReplicationGroup:
  External.external {value: name, namespace: Region.toString(region)}
