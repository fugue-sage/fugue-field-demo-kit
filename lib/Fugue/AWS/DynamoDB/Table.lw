# This is the preferred interface for making DynamoDB resources with
# Fugue. The `Table` and `Projection` modules comprise the bulk of the
# interface.
#
# If you want to manage a DynamoDB table with Fugue, the `new`
# function is the right place to start.

export
  default
  new
  type Schema
  type Throughput
  type LocalSecondaryIndex
  type Index
  type GlobalSecondaryIndex

import Fugue.AWS.Internal.Dictionary as Dictionary
import Fugue.AWS.Region as Region
import Fugue.Core.AWS.Common as AWS
import Fugue.Core.AWS.DynamoDB as DDB

# `new` DynamoDB Table (Constructor)
#
# Call this constructor to create a new Fugue.Core.AWS.DynamoDB.Table value.
#
# Example:
#
#     import Fugue.AWS.DynamoDB as DDB
#     import Fugue.Core.AWS.Common as AWS
#     
#     table: DDB.Table.new {
#       name: "another-table",
#       region: AWS.Us-west-2,
#       attributes: {
#         "PropertyName": DDB.S
#       },
#       schema: {
#         "PropertyName": DDB.HASH
#       },
#       provisionedThroughput: {
#         read: 10,
#         write: 10,
#       },
#       localSecondaryIndexes: {
#         "lastUpdate": {
#           schema: {
#             "lastUpdate": DDB.RANGE,
#           },
#           projection: DDB.Projection.all,
#         }
#       },
#       globalSecondaryIndexes: None,
#       streamViewType: DDB.NewImageView,
#     }
#
# @arg attributes Dictionary mapping attribute name to
# ScalarAttributeType.
#
# @arg name The name of the table.
#
# @arg schema Dictionary mapping key name to KeyType.
#
# @arg provisionedThroughput A record specifying the read and write
# capacity to provision.
#
# @arg localSecondaryIndexes An optional dictionary of index names to
# LocalSecondaryIndex specifications.
#
# @arg globalSecondaryIndexes An optional dictionary of index names to
# GlobalSecondaryIndex specifications.
#
# @arg streamViewType If specified a stream of the given type is
# configured.
#
# @arg region The AWS region in which to create the table.
#
# @return A Fugue.Core.AWS.DynamoDB.Table value.
fun new {
      attributes: Dictionary<DDB.ScalarAttributeType>,
      name: String,
      schema: Schema,
      provisionedThroughput: Throughput,
      localSecondaryIndexes: Optional<Dictionary<LocalSecondaryIndex>>,
      globalSecondaryIndexes: Optional<Dictionary<GlobalSecondaryIndex>>,
      streamViewType: Optional<DDB.StreamViewType>,
      region: AWS.Region
    } -> DDB.Table:
  let toAttr: fun(x): case x of | (k, v) -> DDB.AttributeDefinition {attributeName: k, attributeType: v}
  let toSchema: fun(x): case x of | (k, v) -> DDB.KeySchemaElement {attributeName: k, keyType: v}
  let toLocalIdx: fun(x): case x of
                            | (k, v) -> DDB.LocalSecondaryIndex {
                                          indexName: k,
                                          keySchema: List.map(toSchema, Dictionary.toList(v.schema)),
                                          projection: v.projection,
                                          }
  let toGlobalIdx: fun(x): case x of
                             | (k, v) -> DDB.GlobalSecondaryIndex {
                                           indexName: k,
                                           keySchema: List.map(toSchema, Dictionary.toList(v.schema)),
                                           projection: v.projection,
                                           provisionedThroughput: DDB.ProvisionedThroughput {
                                             readCapacityUnits: v.provisionedThroughput.read,
                                             writeCapacityUnits: v.provisionedThroughput.write,
                                           }
                                         }
  DDB.Table {
    tableName: name,
    region: region,
    attributeDefinitions: List.map(toAttr, Dictionary.toList(attributes)),
    keySchema: List.map(toSchema, Dictionary.toList(schema)),
    provisionedThroughput: DDB.ProvisionedThroughput {
      readCapacityUnits: provisionedThroughput.read,
      writeCapacityUnits:  provisionedThroughput.write,
    },
    localSecondaryIndexes: Optional.map(fun(x): List.map(toLocalIdx, Dictionary.toList(x)), localSecondaryIndexes),
    globalSecondaryIndexes: Optional.map(fun(x): List.map(toGlobalIdx, Dictionary.toList(x)), globalSecondaryIndexes),
    streamViewType: streamViewType,
  }


# DynamoDB Key Schema (Resource Component)
#
# This type of value defines the table's key schema. Schemas include a
# hash key, hash and range keys, etc.
#
# Example:
# 
#     schema: {
#       "PropertyName": DDB.HASH
#     }
# 
#
# This value is given as a dictionary of property name keys to
# `KeyType` values.
type Schema: Dictionary<DDB.KeyType>

# DynamoDB Throughput (Resource Component)
#
# This type of value specifies the throughput to be provisioned for a
# DynamoDB table.
#
# Example:
# 
#     provisionedThroughput: {
#       read: 10,
#       write: 10,
#     }
# 
#
# @field read The read throughput of the table.
# @field write The write throughput of the table.
type Throughput:
  read: Int
  write: Int

# DynamoDB Index Type
#
# This type of value defines both Local and Global secondary indices.
#
# Example (showing `LocalSecondaryIndex`):
# 
#     "lastUpdate": {
#       schema: {
#         "lastUpdate": DDB.RANGE,
#       },
#       projection: DDB.Projection.all,
#     }
# 
#
# @field schema The key schema of the index.
# @field projection The type of data projection to use for the index.
type Index<a>: {
  schema: Schema,
  projection: DDB.Projection,
  a
}

# DynamoDB Local Secondary Index
#
# This type of value defines a local secondary index for a DynamoDB
# table. It is given as a dictionary, with the index name as a key and
# an `Index` value.
#
#
# Example:
# 
#     localSecondaryIndexes: {
#       "lastUpdate": {
#         schema: {
#           "lastUpdate": DDB.RANGE,
#         },
#         projection: DDB.Projection.all,
#       }
#     }
# 
type LocalSecondaryIndex: Index<{}>

# DynamoDB Global Secondary Index
#
# This type of value defines a global secondary index for a
# DynamoDB table. It is given as a dictionary, with the index name as
# a key and an `Index` value. Additionally, the `Index` value will
# also require the following field:
#
# `provisionedThroughput` : `Throughput`
#   The read and write throughput to provision for the index.
#
# Example:
# 
#     globalSecondaryIndexes: {
#       "lastUpdate": {
#         schema: {
#           "lastUpdate": DDB.RANGE,
#         },
#         projection: DDB.Projection.all,
#         provisionedThroughput: { read: 10, write: 10 }
#       }
#     }
# 
type GlobalSecondaryIndex: Index<{provisionedThroughput: Throughput}>

# DynamoDB Table Specification (Default Values)
#
# **DEPRECATED**: Use of `Fugue.AWS.DynamoDB.Table.default` is no
# longer required. It will be removed in a future release.
default: {}
